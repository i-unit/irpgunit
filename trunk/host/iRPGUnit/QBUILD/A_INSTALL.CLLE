      /* =================================================================== */
      /*   iRPGUnit - Build the whole framework. Run First.                  */
      /* =================================================================== */
      /*  Copyright (c) 2013-2019 iRPGUnit Project Team                      */
      /*  All rights reserved. This program and the accompanying materials   */
      /*  are made available under the terms of the Common Public License    */
      /*  v1.0 which accompanies this distribution, and is available at      */
      /*  http://www.eclipse.org/legal/cpl-v10.html                          */
      /* =================================================================== */
      /*   Installation Instructions:                                        */
      /*                                                                     */
      /*   a) Compile the A_INSTALL CL program:                              */
      /*      CRTBNDCL   PGM(RPGUNIT/A_INSTALL) SRCFILE(RPGUNIT1)            */
      /*                                                                     */
      /*   b) Call the installation program:                                 */
      /*      CALL       PGM(RPGUNIT/A_INSTALL) PARM('RPGUNIT')              */
      /*                                                                     */
      /*   c) Optionally delete the installation program:                    */
      /*      DLTPGM     PGM(RPGUNIT/A_INSTALL)                              */
      /*                                                                     */
      /*   d) Optionally run the iRPGUnit self tests:                        */
      /*      CRTBNDCL   PGM(RPGUNIT/A_SELFTEST) SRCFILE(QBUILD)             */
      /*      CALL       PGM(RPGUNIT/A_SELFTEST) PARM('RPGUNIT')             */
      /*      DLTPGM     PGM(RPGUNIT/A_SELFTEST)                             */
      /*                                                                     */
      /*   The possible 'option' values are:                                 */
      /*                                                                     */
      /*   INSTALL  - Compiles all iRPGUnit objects from source. (Default)   */
      /*   CMPMODS  - Compiles only iRPGUnit modules from source.            */
      /*   DLTMODS  - Deletes all iRPGUnit modules.                          */
      /*                                                                     */
      /* =================================================================== */
      /* >>PRE-COMPILER<<                                                    */
      /*   >>CRTCMD<< CRTBNDCL     PGM(&LI/&OB) +                            */
      /*                           SRCFILE(&SL/&SF) +                        */
      /*                           SRCMBR(&SM);                              */
      /*   >>COMPILE<<                                                       */
      /*     >>PARM<< DBGVIEW(*LIST);                                        */
      /*     >>PARM<< OPTION(*EVENTF);                                       */
      /*   >>END-COMPILE<<                                                   */
      /*   >>EXECUTE<<                                                       */
      /* >>END-PRE-COMPILER<<                                                */
      /* =================================================================== */
             PGM        PARM(&LIB &I_OPTION &I_TGTRLS)

             DCL        VAR(&LIB       ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&I_OPTION  ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&I_TGTRLS  ) TYPE(*CHAR) LEN(10)

             DCL        VAR(&OPTION    ) TYPE(*CHAR) LEN(10) VALUE('INSTALL')
             DCL        VAR(&TGTRLS    ) TYPE(*CHAR) LEN(10) VALUE('V7R1M0')

             DCL        VAR(&SRCF      ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SRCF_RUNIT) TYPE(*CHAR) LEN(10) VALUE('QSRC')
             DCL        VAR(&SRCF_LLIST) TYPE(*CHAR) LEN(10) VALUE('QLLIST')
             DCL        VAR(&SRCF_UTEST) TYPE(*CHAR) LEN(10) VALUE('QUNITTEST')
             DCL        VAR(&DBGVIEW   ) TYPE(*CHAR) LEN(10) VALUE('*LIST')
             DCL        VAR(&MODULE    ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MESSAGE   ) TYPE(*CHAR) LEN(100)

             CHGVAR     VAR(&LIB) VALUE(&LIB)
             MONMSG     MSGID(MCH3601) EXEC(DO)
               RMVMSG     PGMQ(*SAME (*)) CLEAR(*ALL)
               SNDPGMMSG  MSGID(CPF9898) MSGF(*LIBL/QCPFMSG) +
                            MSGDTA('Usage: CALL PGM(A_INSTALL) PARM(''targetLibrary'')') +
                            TOPGMQ(*PRV (*PGMBDY *NONE *NONE)) +
                            TOMSGQ(*TOPGMQ) MSGTYPE(*ESCAPE)
             ENDDO

             CHGVAR     VAR(&OPTION) VALUE(&I_OPTION)
             MONMSG     MSGID(MCH3601) EXEC(DO)
               RMVMSG     PGMQ(*SAME (*)) CLEAR(*ALL)
             ENDDO

             CHGVAR     VAR(&TGTRLS) VALUE(&I_TGTRLS)
             MONMSG     MSGID(MCH3601) EXEC(DO)
               RMVMSG     PGMQ(*SAME (*)) CLEAR(*ALL)
             ENDDO

         /* -------------------------------------------- */
         /*  Install iRPGUnit Library.                   */
         /*  Compiles all iRPGUnit objects from source.  */
         /* -------------------------------------------- */
             IF         COND(&OPTION *EQ 'INSTALL') THEN(DO)
               CHGVAR     VAR(&MESSAGE) +
                          VALUE('Successfully compiled the iRPGUnit library: ' *CAT &LIB)
               CALLSUBR   SUBR(INSTALL)
               GOTO       CMDLBL(FINISHED)
             ENDDO

         /* -------------------------------------------- */
         /*  Compile modules.                            */
         /*  Compiles only iRPGUnit modules from source. */
         /* -------------------------------------------- */
             IF         COND(&OPTION *EQ 'CMPMODS') THEN(DO)
               CHGVAR     VAR(&MESSAGE) +
                          VALUE('Successfully compiled modules into library: ' *CAT &LIB)
               CALLSUBR   SUBR(CMPMODS)
               GOTO       CMDLBL(FINISHED)
             ENDDO

         /* -------------------------------------------- */
         /*  Delete modules.                             */
         /*  Deletes all iRPGUnit modules.               */
         /* -------------------------------------------- */
             IF         COND(&OPTION *EQ 'DLTMODS') THEN(DO)
               CHGVAR     VAR(&MESSAGE) +
                          VALUE('Successfully removed modules from library: ' *CAT &LIB)
               CALLSUBR   SUBR(DLTMODS)
               GOTO       CMDLBL(FINISHED)
             ENDDO

             SNDPGMMSG  MSGID(CPF9898) MSGF(*LIBL/QCPFMSG) +
                          MSGDTA('Invalid ''option'' parameter value: ' *CAT &OPTION) +
                          TOPGMQ(*PRV (*PGMBDY *NONE *NONE)) +
                          TOMSGQ(*TOPGMQ) MSGTYPE(*ESCAPE)

         /* -------------------------------------------- */
         /*  FINISHED!                                   */
         /* -------------------------------------------- */
FINISHED:
             SNDPGMMSG  MSGID(CPF9898) MSGF(*LIBL/QCPFMSG) +
                          MSGDTA(&MESSAGE) +
                          TOPGMQ(*PRV (*PGMBDY *NONE *NONE)) +
                          TOMSGQ(*TOPGMQ) MSGTYPE(*INFO)

         /* ============================================ */
         /*  Install iRPGUnit utility.                   */
         /* ============================================ */
             SUBR       SUBR(INSTALL)

               /* - - - - - - - - - - - - - - - - - - - - */
               /*  Create modules.                        */
               /* - - - - - - - - - - - - - - - - - - - - */
               /* CALLSUBR   SUBR(CMPMODS) TODO: obsolete */

               /* - - - - - - - - - - - - - - - - - - - - */
               /*  Create service program RUTESTCASE.     */
               /* - - - - - - - - - - - - - - - - - - - - */
               CRTBNDCL   PGM(QTEMP/MKTESTCASE) SRCFILE(&LIB/&SRCF_RUNIT) SRCMBR(*PGM)
               CALL       PGM(QTEMP/MKTESTCASE) PARM(&LIB &DBGVIEW &TGTRLS)
               /* TODO: obsolete */
               /* CRTSRVPGM  SRVPGM(&LIB/RUTESTCASE) +                                             +
                            MODULE(&LIB/ASSERT     +                                               +
                                   &LIB/PGMMSG     +                                               +
                                   &LIB/VERSION    +                                               +
                                   &LIB/TESTUTILS) +                                               +
                            EXPORT(*SRCFILE) +                                                     +
                            SRCFILE(&LIB/QBND) +                                                   +
                            SRCMBR(RUTESTCASE) +                                                   +
                            ACTGRP(*CALLER) +                                                      +
                            ALWUPD(*YES) +                                                         +
                            ALWLIBUPD(*YES) +                                                      +
                            DETAIL(*NONE) +                                                        +
                            TGTRLS(&TGTRLS) +                                                      +
                            TEXT('iRPGUnit - Test Suite Toolkit.')*/

               /* - - - - - - - - - - - - - - - - - - - - */
               /*  Create commands: RUCALLTST, RUCRTTST   */
               /* - - - - - - - - - - - - - - - - - - - - */
               CRTBNDCL   PGM(QTEMP/MKCMD) SRCFILE(&LIB/QCMD) SRCMBR(*PGM)
               CALL       PGM(QTEMP/MKCMD) PARM(&LIB &DBGVIEW &TGTRLS)

               /* - - - - - - - - - - - - - - - - - - - - */
               /*  Create program RUCALLTST.              */
               /* - - - - - - - - - - - - - - - - - - - - */
               /* CRTBNDCL   PGM(QTEMP/MKCALLTST) SRCFILE(&LIB/&SRCF_RUNIT) SRCMBR(*PGM)           +
               CALL       PGM(QTEMP/MKCALLTST) PARM(&LIB &DBGVIEW &TGTRLS)*/
               /* TODO: obsolete */
               /* CRTPGM     PGM(&LIB/RUCALLTST) +                                                 +
                            MODULE(&LIB/CMDRUN    +                                                +
                                   &LIB/CMDRUNLOG +                                                +
                                   &LIB/CMDRUNPRT +                                                +
                                   &LIB/CMDRUNSRV +                                                +
                                   &LIB/CALLPROC  +                                                +
                                   &LIB/EXTPRC    +                                                +
                                   &LIB/EXTTST    +                                                +
                                   &LIB/LIBL      +                                                +
                                   &LIB/LLIST     +                                                +
                                   &LIB/PGMMSG    +                                                +
                                   &LIB/STRING    +                                                +
                                   &LIB/USRSPC    +                                                +
                                   &LIB/XMLWRITER ) +                                              +
                            ENTMOD(&LIB/CMDRUN) +                                                  +
                            BNDSRVPGM(*LIBL/RUTESTCASE) +                                          +
                            ACTGRP(*NEW) +                                                         +
                            ALWUPD(*YES) +                                                         +
                            ALWLIBUPD(*YES) +                                                      +
                            DETAIL(*NONE) +                                                        +
                            TGTRLS(&TGTRLS) +                                                      +
                            TEXT('iRPGUnit - Execute Test Suite.')                                 +
                                                                                                   +
               CRTPNLGRP  PNLGRP(&LIB/RUCALLTST) +                                                 +
                            SRCFILE(&LIB/QPNLGRP) SRCMBR(*PNLGRP)                                  +
                                                                                                   +
               CRTCMD     CMD(&LIB/RUCALLTST) +                                                    +
                            PGM(*LIBL/RUCALLTST) +                                                 +
                            SRCFILE(&LIB/QCMD) SRCMBR(*CMD) +                                      +
                            HLPPNLGRP(*LIBL/RUCALLTST) +                                           +
                            HLPID(RUCALLTST)                                                      */

               /* - - - - - - - - - - - - - - - - - - - - */
               /*  Create program RUCRTTST.               */
               /* - - - - - - - - - - - - - - - - - - - - */
               /* CRTBNDCL   PGM(QTEMP/MKCRTTST) SRCFILE(&LIB/&SRCF_RUNIT) SRCMBR(*PGM)            +
               CALL       PGM(QTEMP/MKCRTTST) PARM(&LIB &DBGVIEW &TGTRLS)*/
               /* TODO: obsolete */
               /* CRTPGM     PGM(&LIB/RUCRTTST) +                                                  +
                            MODULE(&LIB/CRTTST    +                                                +
                                   &LIB/OBJECT    +                                                +
                                   &LIB/PGMMSG    +                                                +
                                   &LIB/SRCMBR    +                                                +
                                   &LIB/STRING    +                                                +
                                   &LIB/TAGTST    +                                                +
                                   &LIB/USRSPC    ) +                                              +
                            ENTMOD(&LIB/CRTTST) +                                                  +
                            BNDSRVPGM(*LIBL/RUTESTCASE) +                                          +
                            ACTGRP(*NEW) +                                                         +
                            ALWUPD(*YES) +                                                         +
                            ALWLIBUPD(*YES) +                                                      +
                            DETAIL(*NONE) +                                                        +
                            TGTRLS(&TGTRLS) +                                                      +
                            TEXT('iRPGUnit - Create Test Suite.')                                  +
                                                                                                   +
               CRTPNLGRP  PNLGRP(&LIB/RUCRTTST) +                                                  +
                            SRCFILE(&LIB/QPNLGRP) SRCMBR(*PNLGRP)                                  +
                                                                                                   +
               CRTCMD     CMD(&LIB/RUCRTTST) +                                                     +
                            PGM(*LIBL/RUCRTTST) +                                                  +
                            SRCFILE(&LIB/QCMD) SRCMBR(*CMD) +                                      +
                            HLPPNLGRP(*LIBL/RUCRTTST) +                                            +
                            HLPID(RUCRTTST)*/

               /* - - - - - - - - - - - - - - - - - - - - */
               /*  Create program RUPGMRMT.               */
               /* - - - - - - - - - - - - - - - - - - - - */
               CRTBNDCL   PGM(QTEMP/MKRMTRUN) SRCFILE(&LIB/&SRCF_RUNIT) SRCMBR(*PGM)
               CALL       PGM(QTEMP/MKRMTRUN) PARM(&LIB &DBGVIEW &TGTRLS)
               /* TODO: obsolete */
               /* CRTPGM     PGM(&LIB/RUPGMRMT) +                                                  +
                            MODULE(&LIB/PGMRMT    +                                                +
                                   &LIB/CALLPROC  +                                                +
                                   &LIB/CMDRUN    +                                                +
                                   &LIB/CMDRUNSRV +                                                +
                                   &LIB/CMDRUNPRT +                                                +
                                   &LIB/CMDRUNLOG +                                                +
                                   &LIB/EXTPRC    +                                                +
                                   &LIB/EXTTST    +                                                +
                                   &LIB/LIBL      +                                                +
                                   &LIB/LLIST     +                                                +
                                   &LIB/PGMMSG    +                                                +
                                   &LIB/SRCMBR    +                                                +
                                   &LIB/STRING    +                                                +
                                   &LIB/USRSPC    +                                                +
                                   &LIB/RMTRUNSRV +                                                +
                                   &LIB/XMLWRITER ) +                                              +
                            ENTMOD(&LIB/PGMRMT) +                                                  +
                            BNDSRVPGM(*LIBL/RUTESTCASE) +                                          +
                            ACTGRP(*NEW) +                                                         +
                            ALWUPD(*YES) +                                                         +
                            ALWLIBUPD(*YES) +                                                      +
                            DETAIL(*NONE) +                                                        +
                            TGTRLS(&TGTRLS) +                                                      +
                            TEXT('iRPGUnit - Plug-in Test Runner.')*/

               /* - - - - - - - - - - - - - - - - - - - - */
               /*  Create command BUILD.                  */
               /* - - - - - - - - - - - - - - - - - - - - */

               CRTPNLGRP  PNLGRP(&LIB/BUILDHLP) +
                            SRCFILE(&LIB/QBUILD) SRCMBR(*PNLGRP)

               CRTCMD     CMD(&LIB/BUILD) +
                            PGM(*LIBL/A_INSTALL) +
                            SRCFILE(&LIB/QBUILD) SRCMBR(*CMD) +
                            HLPPNLGRP(*LIBL/BUILDHLP) +
                            HLPID(BUILD)

               CRTBNDCL   PGM(&LIB/A_INSTALL) +
                            SRCFILE(&LIB/QBUILD) SRCMBR(*PGM) +
                            DFTACTGRP(*NO) +
                            ACTGRP(*CALLER) +
                            TGTRLS(&TGTRLS)

               /* - - - - - - - - - - - - - - - - - - - - */
               /*  Create translation table RUWSCST.      */
               /* - - - - - - - - - - - - - - - - - - - - */
               CRTWSCST   WSCST(&LIB/RUWSCST) +
                            SRCFILE(&LIB/QSRC)

               DLTJOBD    JOBD(&LIB/RPGUNIT)
               MONMSG     MSGID(CPF2105)  /* Object not found */

               CRTJOBD    JOBD(&LIB/RPGUNIT) +
                            TEXT('iRPGUnit - Library List For Test Cases') +
                            INLLIBL(&LIB)

               /* - - - - - - - - - - - - - - - - - - - - */
               /*  Delete modules.                        */
               /* - - - - - - - - - - - - - - - - - - - - */
               CALLSUBR   SUBR(DLTMODS)

             ENDSUBR

         /* ============================================ */
         /*  Compile modules.                            */
         /* ============================================ */
             SUBR       SUBR(CMPMODS)

               CHGVAR     VAR(&SRCF) VALUE(&SRCF_RUNIT)
               CHGVAR     VAR(&MODULE) VALUE('ASSERT')
               CALLSUBR   SUBR(CMPRPGMOD)
               CHGVAR     VAR(&MODULE) VALUE('CALLPROC')
               CALLSUBR   SUBR(CMPRPGMOD)
               CHGVAR     VAR(&MODULE) VALUE('CMDRUN')
               CALLSUBR   SUBR(CMPRPGMOD)
               CHGVAR     VAR(&MODULE) VALUE('CMDRUNLOG')
               CALLSUBR   SUBR(CMPRPGMOD)
               CHGVAR     VAR(&MODULE) VALUE('CMDRUNPRT')
               CALLSUBR   SUBR(CMPRPGMOD)
               CHGVAR     VAR(&MODULE) VALUE('CMDRUNSRV')
               CALLSUBR   SUBR(CMPRPGMOD)
               CHGVAR     VAR(&MODULE) VALUE('CRTTST')
               CALLSUBR   SUBR(CMPRPGMOD)
               CHGVAR     VAR(&MODULE) VALUE('EXTPRC')
               CALLSUBR   SUBR(CMPRPGMOD)
               CHGVAR     VAR(&MODULE) VALUE('EXTTST')
               CALLSUBR   SUBR(CMPRPGMOD)
               CHGVAR     VAR(&MODULE) VALUE('LIBL')
               CALLSUBR   SUBR(CMPRPGMOD)
               CHGVAR     VAR(&MODULE) VALUE('OBJECT')
               CALLSUBR   SUBR(CMPRPGMOD)
               CHGVAR     VAR(&MODULE) VALUE('PGMMSG')
               CALLSUBR   SUBR(CMPRPGMOD)
               CHGVAR     VAR(&MODULE) VALUE('PGMRMT')
               CALLSUBR   SUBR(CMPRPGMOD)
               CHGVAR     VAR(&MODULE) VALUE('RMTRUNSRV')
               CALLSUBR   SUBR(CMPRPGMOD)
               CHGVAR     VAR(&MODULE) VALUE('SRCMBR')
               CALLSUBR   SUBR(CMPRPGMOD)
               CHGVAR     VAR(&MODULE) VALUE('STRING')
               CALLSUBR   SUBR(CMPRPGMOD)
               CHGVAR     VAR(&MODULE) VALUE('TAGTST')
               CALLSUBR   SUBR(CMPRPGMOD)
               CHGVAR     VAR(&MODULE) VALUE('TESTUTILS')
               CALLSUBR   SUBR(CMPSQLMOD)
               CHGVAR     VAR(&MODULE) VALUE('USRSPC')
               CALLSUBR   SUBR(CMPRPGMOD)
               CHGVAR     VAR(&MODULE) VALUE('VERSION')
               CALLSUBR   SUBR(CMPRPGMOD)
               CHGVAR     VAR(&MODULE) VALUE('XMLWRITER')
               CALLSUBR   SUBR(CMPRPGMOD)

               CHGVAR     VAR(&SRCF) VALUE(&SRCF_LLIST)
               CHGVAR     VAR(&MODULE) VALUE('LLIST')
               CALLSUBR   SUBR(CMPRPGMOD)

               CHGVAR     VAR(&SRCF) VALUE(&SRCF_UTEST)
               CHGVAR     VAR(&MODULE) VALUE('LIBLC')
               CALLSUBR   SUBR(CMPCLMOD)
               CHGVAR     VAR(&MODULE) VALUE('SPLF')
               CALLSUBR   SUBR(CMPRPGMOD)

             ENDSUBR

         /* ============================================ */
         /*  Compile RPG module.                         */
         /* ============================================ */
             SUBR       SUBR(CMPRPGMOD)

               CRTRPGMOD  MODULE(&LIB/&MODULE) +
                            SRCFILE(&LIB/&SRCF) SRCMBR(*MODULE) +
                            DBGVIEW(&DBGVIEW) TRUNCNBR(*NO) TGTRLS(&TGTRLS)
               DLTSPLF    FILE(&MODULE) JOB(*) SPLNBR(*LAST)
               CHGVAR     VAR(&MODULE) VALUE(' ')

             ENDSUBR

         /* ============================================ */
         /*  Compile SQL module.                         */
         /* ============================================ */
             SUBR       SUBR(CMPSQLMOD)

               CRTSQLRPGI OBJ(&LIB/&MODULE) +
                            SRCFILE(&LIB/&SRCF) SRCMBR(*OBJ) +
                            OBJTYPE(*MODULE) RPGPPOPT(*LVL2) +
                            DBGVIEW(*NONE) OUTPUT(*NONE) TGTRLS(&TGTRLS) +
                            COMPILEOPT('TRUNCNBR(*NO) DBGVIEW(*ALL)')
               DLTSPLF    FILE(&MODULE) JOB(*) SPLNBR(*LAST)
               DLTSPLF    FILE(&MODULE) JOB(*) SPLNBR(*LAST)
               CHGVAR     VAR(&MODULE) VALUE(' ')

             ENDSUBR

         /* ============================================ */
         /*  Compile CL module.                          */
         /* ============================================ */
             SUBR       SUBR(CMPCLMOD)

               CRTCLMOD   MODULE(&LIB/&MODULE) SRCFILE(&LIB/&SRCF) +
                            SRCMBR(*MODULE) DBGVIEW(*LIST) TGTRLS(&TGTRLS)
               DLTSPLF    FILE(&MODULE) JOB(*) SPLNBR(*LAST)
               CHGVAR     VAR(&MODULE) VALUE(' ')

             ENDSUBR

         /* ============================================ */
         /*  Delete modules.                             */
         /* ============================================ */
             SUBR       SUBR(DLTMODS)

               CHGVAR     VAR(&MODULE) VALUE(ASSERT)
               CALLSUBR   SUBR(DLTMOD)
               CHGVAR     VAR(&MODULE) VALUE(CALLPROC)
               CALLSUBR   SUBR(DLTMOD)
               CHGVAR     VAR(&MODULE) VALUE(CMDRUN)
               CALLSUBR   SUBR(DLTMOD)
               CHGVAR     VAR(&MODULE) VALUE(CMDRUNLOG)
               CALLSUBR   SUBR(DLTMOD)
               CHGVAR     VAR(&MODULE) VALUE(CMDRUNPRT)
               CALLSUBR   SUBR(DLTMOD)
               CHGVAR     VAR(&MODULE) VALUE(CMDRUNSRV)
               CALLSUBR   SUBR(DLTMOD)
               CHGVAR     VAR(&MODULE) VALUE(CRTTST)
               CALLSUBR   SUBR(DLTMOD)
               CHGVAR     VAR(&MODULE) VALUE(EXTPRC)
               CALLSUBR   SUBR(DLTMOD)
               CHGVAR     VAR(&MODULE) VALUE(EXTTST)
               CALLSUBR   SUBR(DLTMOD)
               CHGVAR     VAR(&MODULE) VALUE(LIBL)
               CALLSUBR   SUBR(DLTMOD)
               CHGVAR     VAR(&MODULE) VALUE(OBJECT)
               CALLSUBR   SUBR(DLTMOD)
               CHGVAR     VAR(&MODULE) VALUE(PGMMSG)
               CALLSUBR   SUBR(DLTMOD)
               CHGVAR     VAR(&MODULE) VALUE(PGMRMT)
               CALLSUBR   SUBR(DLTMOD)
               CHGVAR     VAR(&MODULE) VALUE(RMTRUNSRV)
               CALLSUBR   SUBR(DLTMOD)
               CHGVAR     VAR(&MODULE) VALUE(SRCMBR)
               CALLSUBR   SUBR(DLTMOD)
               CHGVAR     VAR(&MODULE) VALUE(STRING)
               CALLSUBR   SUBR(DLTMOD)
               CHGVAR     VAR(&MODULE) VALUE(TAGTST)
               CALLSUBR   SUBR(DLTMOD)
               CHGVAR     VAR(&MODULE) VALUE(TESTUTILS)
               CALLSUBR   SUBR(DLTMOD)
               CHGVAR     VAR(&MODULE) VALUE(USRSPC)
               CALLSUBR   SUBR(DLTMOD)
               CHGVAR     VAR(&MODULE) VALUE(VERSION)
               CALLSUBR   SUBR(DLTMOD)
               CHGVAR     VAR(&MODULE) VALUE(XMLWRITER)
               CALLSUBR   SUBR(DLTMOD)

               CHGVAR     VAR(&MODULE) VALUE(LLIST)
               CALLSUBR   SUBR(DLTMOD)

               CHGVAR     VAR(&MODULE) VALUE(LIBLC)
               CALLSUBR   SUBR(DLTMOD)
               CHGVAR     VAR(&MODULE) VALUE(SPLF)
               CALLSUBR   SUBR(DLTMOD)

             ENDSUBR

         /* ============================================ */
         /*  Delete module.                              */
         /* ============================================ */
             SUBR       SUBR(DLTMOD)

               DLTMOD     MODULE(&LIB/&MODULE)
               MONMSG     MSGID(CPF2105) EXEC(DO)  /* Object not found */
                 RCVMSG     PGMQ(*SAME (*)) MSGTYPE(*EXCP) RMV(*YES)
               ENDDO

             ENDSUBR

             ENDPGM
