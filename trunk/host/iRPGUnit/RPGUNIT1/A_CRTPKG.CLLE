     /* =================================================================== */
     /*  RPGUnit - Create distribution package.                             */
     /* =================================================================== */
     /*  Updates the MKMETADATA source member and saves the                 */
     /*  RPGUNIT library to save file RPGUNIT/RPGUNIT.                      */
     /* =================================================================== */
     /*   >>PRE-COMPILER<<                                                  */
     /*     >>CRTCMD<<  CRTBNDCL      PGM(&LI/&OB) +                        */
     /*                               SRCFILE(&SL/&SF)  +                   */
     /*                               SRCMBR(&SM);                          */
     /*     >>COMPILE<<                                                     */
     /*       >>PARM<< DBGVIEW(*LIST);                                      */
     /*     >>END-COMPILE<<                                                 */
     /*     >>LINK<<                                                        */
     /*       >>PARM<< DFTACTGRP(*NO);                                      */
     /*       >>PARM<< ACTGRP(*NEW);                                        */
     /*     >>END-LINK<<                                                    */
     /*     >>EXECUTE<<                                                     */
     /*   >>END-PRE-COMPILER<<                                              */
     /* =================================================================== */
             PGM

             DCL        VAR(&LIB       ) TYPE(*CHAR) LEN(10) VALUE('RPGUNIT')
             DCL        VAR(&SAVF      ) TYPE(*CHAR) LEN(10) VALUE('RPGUNIT')
             DCL        VAR(&TGTRLS    ) TYPE(*CHAR) LEN(10) VALUE('V6R1M0')
             DCL        VAR(&TEXT      ) TYPE(*CHAR) LEN(50)
             DCL        VAR(&RELEASE   ) TYPE(*CHAR) LEN(20)

             /* ---------------------------------------------------- */
             /*  Create member list of RPGUNIT1 and get release#.    */
             /* ---------------------------------------------------- */
             CRTRPGMOD  MODULE(QTEMP/CRTMETADTA) +
                          SRCFILE(*LIBL/RPGUNIT1) SRCMBR(*MODULE) +
                          DBGVIEW(*LIST) TRUNCNBR(*NO)
             CRTPGM     PGM(QTEMP/CRTMETADTA) BNDDIR(QC2LE) +
                          DETAIL(*BASIC)
             DLTMOD     MODULE(QTEMP/CRTMETADTA)

             CALL       PGM(QTEMP/CRTMETADTA) PARM(&LIB &RELEASE)

             /* ---------------------------------------------------- */
             /*  Re-create objects for the specified target release  */
             /* ---------------------------------------------------- */
             CRTBNDCL   PGM(QTEMP/MKLLIST) SRCFILE(RPGUNIT1)
             CALL       PGM(QTEMP/MKLLIST) PARM(&LIB &TGTRLS)

             CRTBNDCL   PGM(QTEMP/MKTESTCASE) SRCFILE(RPGUNIT1)
             CALL       PGM(QTEMP/MKTESTCASE) PARM(&LIB &TGTRLS)

             CRTBNDCL   PGM(QTEMP/MKCRTTST) SRCFILE(RPGUNIT1)
             CALL       PGM(QTEMP/MKCRTTST) PARM(&LIB &TGTRLS)

             CRTBNDCL   PGM(QTEMP/MKCALLTST) SRCFILE(RPGUNIT1)
             CALL       PGM(QTEMP/MKCALLTST) PARM(&LIB &TGTRLS)

             CRTBNDCL   PGM(QTEMP/MKRMTRUN) SRCFILE(RPGUNIT1)
             CALL       PGM(QTEMP/MKRMTRUN) PARM(&LIB &TGTRLS)

             /* ---------------------------------------------------- */
             /*  Save install package to save file                   */
             /* ---------------------------------------------------- */
             CHGVAR     VAR(&TEXT) +
                          VALUE('RPGUNIT Distribution Package - ' *CAT &RELEASE)
             CHGLIB     LIB(&LIB) TEXT(&TEXT)

             DLTF       FILE(QTEMP/&SAVF)
             MONMSG     MSGID(CPF2105)

             CRTSAVF    FILE(QTEMP/&SAVF) TEXT(&TEXT)

      /*        SAVLIB     LIB(&LIB) DEV(*SAVF) SAVF(QTEMP/&SAVF) +            */
      /*                     TGTRLS(&TGTRLS) DTACPR(*HIGH) +                   */
      /*                     OMITOBJ((&LIB/*ALL     *PGM   )  +                */
      /*                          (&LIB/*ALL     *SRVPGM)  +                   */
      /*                          (&LIB/*ALL     *MODULE)  +                   */
      /*                          (&LIB/*ALL     *CMD   )  +                   */
      /*                          (&LIB/*ALL     *DTAARA)  +                   */
      /*                          (&LIB/*ALL     *USRSPC)  +                   */
      /*                          (&LIB/*ALL     *JOBD  )  +                   */
      /*                          (&LIB/*ALL     *PNLGRP)  +                   */
      /*                          (&LIB/*ALL     *WSCST )  +                   */
      /*                          (&LIB/&SAVF    *FILE  )  +                   */
      /*                             (&LIB/EVFEVENT *FILE  ))                  */

             SAVLIB     LIB(&LIB) DEV(*SAVF) SAVF(QTEMP/&SAVF) +
                          TGTRLS(&TGTRLS) DTACPR(*HIGH) +
                          SELECT( +
                            (*INCLUDE &LIB/RUCALLTST  *PGM)    +
                            (*INCLUDE &LIB/RUCRTTST   *PGM)    +
                            (*INCLUDE &LIB/RUPGMRMT   *PGM)    +
                            (*INCLUDE &LIB/RUTAGTST   *PGM)    +
                            (*INCLUDE &LIB/LLIST      *SRVPGM) +
                            (*INCLUDE &LIB/RURUNNER   *SRVPGM) +
                            (*INCLUDE &LIB/RURUNRMT   *SRVPGM) +
                            (*INCLUDE &LIB/RUTESTCASE *SRVPGM) +
                            (*INCLUDE &LIB/RPGUNITC1  *FILE)   +
                            (*INCLUDE &LIB/RPGUNITF1  *FILE)   +
                            (*INCLUDE &LIB/RPGUNITT1  *FILE)   +
                            (*INCLUDE &LIB/RPGUNITY1  *FILE)   +
                            (*INCLUDE &LIB/RPGUNIT1   *FILE)   +
                            (*INCLUDE &LIB/RPGUNIT    *JOBD)   +
                            (*INCLUDE &LIB/RUCALLTST  *CMD)    +
                            (*INCLUDE &LIB/RUCRTTST   *CMD)    +
                            (*INCLUDE &LIB/RUCMDHLP   *PNLGRP) +
                            (*INCLUDE &LIB/RUWSCST    *WSCST))

             DLTF       FILE(&LIB/&SAVF)
             MONMSG     MSGID(CPF2105)

             MOVOBJ     OBJ(QTEMP/&SAVF) OBJTYPE(*FILE) TOLIB(&LIB)

             DLTPGM     PGM(QTEMP/CRTMETADTA)

             ENDPGM
