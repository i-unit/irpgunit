/* =================================================================== */
/*  Utility . . . : RPGUnit                                            */
/*  Object  . . . : RUNRMT                                             */
/*  Type  . . . . : *PGM                                               */
/*  Description . : RPGUnit - Test program: RUPGMRMT.                  */
/* =================================================================== */
/*  History:                                                           */
/*                                                                     */
/*  Datum       Name          Änderung                                 */
/*  ----------  ------------  ---------------------------------------  */
/*  11.11.2013  Th.Raddatz    Changed parameter PROCNAME accept up     */
/*                            to 250 procedure names.                  */
/*                                                                     */
/*  06.01.2014  Th.Raddatz    Added optional parameter &I_PROCNAME.    */
/*                                                                     */
/* =================================================================== */
/* >>PRE-COMPILER<<                                                    */
/*   >>CRTCMD<< CRTBNDCL     PGM(&LI/&OB) +                            */
/*                           SRCFILE(&SL/&SF) +                        */
/*                           SRCMBR(&SM);                              */
/*   >>COMPILE<<                                                       */
/*     >>PARM<< DBGVIEW(*LIST);                                        */
/*   >>END-COMPILE<<                                                   */
/*   >>EXECUTE<<                                                       */
/* >>END-PRE-COMPILER<<                                                */
/* =================================================================== */
             PGM        PARM(&SRVPGM &I_PROCNAME)

             DCL        VAR(&SRVPGM    ) TYPE(*CHAR) LEN( 10)
             DCL        VAR(&LIB       ) TYPE(*CHAR) LEN( 10)
             DCL        VAR(&I_PROCNAME) TYPE(*CHAR) LEN( 32)

             DCL        VAR(&USERSPACE ) TYPE(*CHAR) LEN( 20)
             DCL        VAR(&EXTATTR   ) TYPE(*CHAR) LEN( 10)
             DCL        VAR(&INITSIZE  ) TYPE(*CHAR) LEN(  4)
             DCL        VAR(&INITVALUE ) TYPE(*CHAR) LEN(  1)
             DCL        VAR(&PUBAUTH   ) TYPE(*CHAR) LEN( 10)
             DCL        VAR(&TEXT      ) TYPE(*CHAR) LEN( 50)
             DCL        VAR(&REPLACE   ) TYPE(*CHAR) LEN( 10)
             DCL        VAR(&ERRORCODE ) TYPE(*CHAR) LEN(  8)

             DCL        VAR(&PROCNAME  ) TYPE(*CHAR) LEN( 32)
             DCL        VAR(&LENGTH    ) TYPE(*INT )
             DCL        VAR(&TESTSUITE ) TYPE(*CHAR) LEN( 20)
             DCL        VAR(&ORDER     ) TYPE(*CHAR) LEN( 10)
             DCL        VAR(&DETAIL    ) TYPE(*CHAR) LEN( 10)
             DCL        VAR(&OUTPUT    ) TYPE(*CHAR) LEN( 10)
             DCL        VAR(&LIBL      ) TYPE(*CHAR) LEN( 12)
             DCL        VAR(&JOBD      ) TYPE(*CHAR) LEN( 20)
             DCL        VAR(&PROCNAMES ) TYPE(*CHAR) LEN(260)
             DCL        VAR(&RC        ) TYPE(*INT )

             CHGVAR     VAR(&PROCNAME) VALUE(&I_PROCNAME)
             MONMSG     MSGID(MCH3601) EXEC(DO)
                RMVMSG     PGMQ(*SAME (*)) CLEAR(*ALL)
                CHGVAR     VAR(&PROCNAME) VALUE('*ALL')
             ENDDO

             CHGVAR     VAR(&LIB) VALUE('*LIBL')

             CHGVAR     VAR(&USERSPACE) VALUE('RESULT    RPGUNIT   ')
             CHGVAR     VAR(&EXTATTR  ) VALUE('RPGUNIT')
             CHGVAR     VAR(&INITSIZE ) VALUE(x'00080000')
             CHGVAR     VAR(&INITVALUE) VALUE(x'00')
             CHGVAR     VAR(&PUBAUTH  ) VALUE('*ALL')
             CHGVAR     VAR(&TEXT     ) VALUE('RPGUnit - Userspace for Result of Unit Test')
             CHGVAR     VAR(&REPLACE  ) VALUE('*YES')
             CHGVAR     VAR(&ERRORCODE) VALUE(x'0000000000000000')

             CHGVAR     VAR(&TESTSUITE) VALUE(&SRVPGM *CAT &LIB)
             CHGVAR     VAR(&ORDER    ) VALUE('*API')
             CHGVAR     VAR(&DETAIL   ) VALUE('*BASIC')
             CHGVAR     VAR(&OUTPUT   ) VALUE('*ALLWAYS')
             CHGVAR     VAR(&JOBD     ) VALUE('*DFT')

             CHGVAR     VAR(%bin(&LIBL 1  2)) VALUE(1)
             CHGVAR     VAR(%sst(&LIBL 3 10)) VALUE('*CURRENT')

             CHGVAR     VAR(&LENGTH) VALUE(1)
             DOWHILE    COND(%SST(&PROCNAME &LENGTH 1) *NE ' ' *AND +
                             &LENGTH *LE 32)
               CHGVAR     VAR(&LENGTH) VALUE(&LENGTH + 1)
             ENDDO

             CHGVAR     VAR(%bin(&PROCNAMES 1   2)) VALUE(1)         /* number of proc name */
             CHGVAR     VAR(%bin(&PROCNAMES 3   2)) VALUE(&LENGTH-1) /* proc name 1, length */
             CHGVAR     VAR(%sst(&PROCNAMES 5 256)) VALUE(&PROCNAME) /* proc name 1, name   */

             CALL       PGM(QUSCRTUS) +
                          PARM(&USERSPACE &EXTATTR &INITSIZE +
                               &INITVALUE &PUBAUTH &TEXT &REPLACE &ERRORCODE)

             CALL       PGM(RUPGMRMT) PARM(&USERSPACE &TESTSUITE +
                          &ORDER &DETAIL &OUTPUT &LIBL &JOBD &PROCNAMES &RC)

         /*  CALL       PGM(QUSDLTUS) PARM(&USERSPACE &ERRORCODE)  */

             ENDPGM
